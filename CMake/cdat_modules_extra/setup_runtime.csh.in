# Main install prefix set by user or post install script:
# UVCDAT_INSTALL_PREFIX

# First reset any existing UVCDAT env
. @CMAKE_INSTALL_PREFIX@/bin/reset_runtime.csh

# Now store existing env var that we will be tweaking
foreach v ( PATH LD_LIBRARY_PATH DYLD_LIBRARY_PATH PYTHONPATH OPAL_PREFIX LIBOVERLAY_SCROLLBAR )
    if ( `eval echo \$\?$v` ) then
        set vv=`eval echo \$$v`
        setenv UVCDAT_ORIGINAL_${v} ${vv}
    else
        setenv UVCDAT_ORIGINAL_${v} " "
    endif
end

setenv UVCDAT_PROMPT_STRING @UVCDAT_PROMPT_STRING@
if ( $?UVCDAT_PROMPT_BEGINNING ) then
  setenv UVCDAT_ORIGINAL_prompt ${prompt}
  set prompt = "[@UVCDAT_PROMPT_STRING@]${prompt}"
else if ( $?UVCDAT_PROMPT_END ) then
  setenv UVCDAT_ORIGINAL_prompt ${prompt}
  set prompt = "${prompt}[@UVCDAT_PROMPT_STRING@]"
endif

# If unset, use the value configured by cmake by default.

# Everything beyond this point will be determined relatively
# from this path.
if ( $?UVCDAT_INSTALL_PREFIX ) then
  set install_prefix=${UVCDAT_INSTALL_PREFIX}
else
  set install_prefix=@CMAKE_INSTALL_PREFIX@
endif

# Try to prevent the user from sourcing twice,
# which can lead to errors.
if ( $?UVCDAT_SETUP_PATH ) then
  if ( ${UVCDAT_SETUP_PATH} == ${install_prefix} ) then
    echo 'Nothing to do since UVCDAT is already setup at '${UVCDAT_SETUP_PATH}
    exit 0
  else
    echo 'ERROR: UVCDAT setup was previously sourced at '${UVCDAT_SETUP_PATH}
    echo 'ERROR: There is no need to run setup_runtime manually anymore.'
    echo 'ERROR: Open a new shell in order to use a different install location.'
    echo 'ERROR: Or execute source @CMAKE_INSTALL_PREFIX@/bin/reset_runtime.sh.'
    exit 1
  endif
endif

# Check that the install prefix exists, otherwise stop.
if ( ! -d ${install_prefix} ) then
  echo 'ERROR: '${install_prefix}' is not a directory.'
  exit 1
endif

if ( ! $?LD_LIBRARY_PATH ) then
  setenv LD_LIBRARY_PATH ''
endif

if ( ! $?PYTHONPATH ) then
  setenv PYTHONPATH ''
endif

if ( ! $?PATH ) then
  setenv PATH ''
endif

if ( '@QT_LIB_DIR@' != '' ) then
  if ( -d @QT_LIB_DIR@ ) then
    setenv LD_LIBRARY_PATH @QT_LIB_DIR@:${LD_LIBRARY_PATH}
  endif
endif

foreach d ( @SETUP_LIBRARY_PATHS@ )
  set f=${install_prefix}/${d}
  if ( -d ${f} ) then
    setenv LD_LIBRARY_PATH ${f}:${LD_LIBRARY_PATH}
  endif
end

if ( `uname` == 'Darwin' ) then
  setenv LD_LIBRARY_PATH /usr/X11R6/lib:${LD_LIBRARY_PATH}
  setenv DYLD_FALLBACK_LIBRARY_PATH ${LD_LIBRARY_PATH}
endif

foreach d ( @SETUP_PYTHON_PATHS@ )
  set f=${install_prefix}/${d}
  if ( -d ${f} ) then
    setenv PYTHONPATH ${f}:${PYTHONPATH}
  endif
end

foreach d ( @SETUP_EXECUTABLE_PATHS@ )
  set f=${install_prefix}/${d}
  if ( -d ${f} ) then
    setenv PATH ${f}:${PATH}
  endif
end

if ( -d ${install_prefix}/Externals/lib/R ) then
  setenv R_HOME ${install_prefix}/Externals/lib/R
endif

setenv OPAL_PREFIX ${install_prefix}/Externals
setenv LIBOVERLAY_SCROLLBAR 0

setenv UVCDAT_SETUP_PATH ${install_prefix}

unset install_prefix

echo 'Successfully updated your environment to use UVCDAT'
echo '(changes are valid for this session/terminal only)'
echo 'Version: '${UVCDAT_PROMPT_STRING}
echo 'Location: '${UVCDAT_SETUP_PATH}
echo 'Reset these changes by running: source @CMAKE_INSTALL_PREFIX@/bin/reset_runtime.csh'
